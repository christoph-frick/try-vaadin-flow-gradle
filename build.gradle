plugins {
    id 'org.springframework.boot' version "${springbootGradlePluginVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementGradlePluginVersion}"
    id 'com.vaadin' version "${vaadinGradlePluginVersion}"
    id 'groovy'
}

group = 'net.ofnir'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = "https://maven.vaadin.com/vaadin-prereleases" }
    maven { url 'https://maven.vaadin.com/vaadin-addons' }
}

bootRun {
    environment("SPRING_PROFILES_ACTIVE", "dev")
    systemProperties = System.properties
}

bootJar {
    dependsOn vaadinBuildFrontend
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation('org.springframework.boot:spring-boot-starter-validation')
    implementation 'org.codehaus.groovy:groovy'
    implementation enforcedPlatform("com.vaadin:vaadin-bom:${getAt("vaadin.version")}")
    implementation("com.vaadin:vaadin-core") {
        [
                "com.vaadin.webjar",
                "org.webjars.bowergithub.insites",
                "org.webjars.bowergithub.polymer",
                "org.webjars.bowergithub.polymerelements",
                "org.webjars.bowergithub.vaadin",
                "org.webjars.bowergithub.webcomponents"
        ].each { exclude group: it }
    }
    implementation("com.vaadin:vaadin-spring-boot-starter")
    implementation("org.github.legioth:field:0.10.0.1")
}

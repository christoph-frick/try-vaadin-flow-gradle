plugins {
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.vaadin' version '0.17.0.1'
    id 'groovy'
}

group = 'net.ofnir'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenLocal()
    mavenCentral()
}

bootRun {
    environment("SPRING_PROFILES_ACTIVE", "dev")
    systemProperties = System.properties
}

bootJar {
    dependsOn vaadinBuildFrontend
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    implementation 'org.codehaus.groovy:groovy'
    implementation enforcedPlatform("com.vaadin:vaadin-bom:${getAt("vaadin.version")}")
    implementation("com.vaadin:vaadin-core") {
        [
                "com.vaadin.webjar",
                "org.webjars.bowergithub.insites",
                "org.webjars.bowergithub.polymer",
                "org.webjars.bowergithub.polymerelements",
                "org.webjars.bowergithub.vaadin",
                "org.webjars.bowergithub.webcomponents"
        ].each { exclude group: it }
    }
    implementation("com.vaadin:vaadin-spring-boot-starter")
}
